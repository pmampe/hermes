/*
 * Copyright (c) 2013, IT Services, Stockholm University
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 * Redistributions of source code must retain the above copyright notice, this
 * list of conditions and the following disclaimer.
 *
 * Redistributions in binary form must reproduce the above copyright notice,
 * this list of conditions and the following disclaimer in the documentation
 * and/or other materials provided with the distribution.
 *
 * Neither the name of Stockholm University nor the names of its contributors
 * may be used to endorse or promote products derived from this software
 * without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
 * LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
 * CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
 * SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
 * INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
 * CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
 * ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
 * POSSIBILITY OF SUCH DAMAGE.
 */





import java.nio.charset.Charset;

import groovy.json.JsonSlurper
import javax.net.ssl.HttpsURLConnection

import org.apache.http.auth.AuthScope;
import org.apache.http.auth.UsernamePasswordCredentials;
import org.apache.http.client.methods.HttpPut
import org.apache.http.impl.auth.BasicScheme;
import org.apache.http.impl.client.AbstractHttpClient;
import org.apache.http.impl.client.DefaultHttpClient
import org.apache.http.entity.mime.MultipartEntity
import org.apache.http.entity.mime.HttpMultipartMode;
import org.apache.http.entity.mime.content.FileBody
import org.apache.http.entity.mime.content.StringBody
import org.apache.http.HttpHost;
import org.apache.http.HttpVersion
import org.apache.http.params.CoreProtocolPNames
import org.apache.http.protocol.BasicHttpContext;
import org.apache.http.HttpResponse
import org.apache.http.util.EntityUtils
import org.apache.tools.ant.filters.*

import com.eriwen.gradle.js.JsMinifier
import com.eriwen.gradle.css.CssMinifier

version = "0.0.4"
group = "se.su.it.handheld.app"
archivesBaseName = "hermes"
description = """Stockholm University mobile app (alfa)."""

def archiveFile = 'su.tar.gz'
def phonegapAppId = '219084'
def phonegapUser = 'utveckling@it.su.se'
def phonegapPassword = System.getenv('PG_PASS')
def phonegapKey = System.getenv('PG_KEY')
def androidCertKeyId = 19331
def iOSCertKeyId = 68333

def phantomjs = {
  String phantomjsBin = System.getenv('PHANTOMJS')
  if (!phantomjsBin) {
    phantomjsBin = System.getenv('PHANTOMJS_HOME')
    if (phantomjsBin) phantomjsBin += "/bin/phantomjs"
  }

  if (!phantomjsBin)
    phantomjsBin = "which phantomjs".execute().text.trim()

  if (!phantomjsBin)
    throw new IllegalArgumentException("No phantomjs found! Specify phantomjs path using \$PHANTOMJS or \$PHANTOMJS_HOME")

  if ("${phantomjsBin} -v".execute().text.trim() < "1.7.0")
    throw new IllegalArgumentException("Too old version of phantomjs found! Please use version 1.7.0 or later.")

  phantomjsBin
}

apply plugin: 'saga'
apply plugin: 'js'
apply plugin: 'css'

/**
 * Wrapper
 */

task wrapper(type: Wrapper) {
  gradleVersion = '1.5'
}

javascript.source {
  prod {
    js {
      srcDir file('assets/www/')
      include "**/*.js"
      exclude '**/cordova*.js', '**/GAPlugin*.js', '**/*.min.js', '**/*-min.js', '**/spec/*'
    }
  }
}

css.source {
  prod {
    css {
      srcDir file('assets/www')
      include '**/*.css'
      exclude '**/*.min.css', '**/spec/*'
    }
  }
}

/**
 * Coverage
 */

saga {
  baseDir = file('assets/www/')
  outputDir = file('build/reports/coverage')
  includes = '**/*spec.html'
  noInstrumentPatterns = [".*/cordova.*\\.js", ".*/GAPlugin.*\\.js", ".+\\/lib\\/.*", ".+\\/spec\\/.*"]
  sourcesToPreload = "**/*.js"
}

coverage.doLast {
  "cp -v build/reports/coverage/total-coverage.dat build/reports/coverage/jsTestDriver.conf-coverage.dat".execute()
}

/**
 * Sonar integration
 */

task buildSonarPom(type: Copy) {
  from 'pom.xml.dist'
  into '.'

  rename(/(.+).dist/, '$1')
  filter(ReplaceTokens, tokens: [
          group: project.group,
          name: project.archivesBaseName,
          version: project.version,
          description: project.description
  ])
}

task minify() {
  doLast {
    javascript.source.prod.js.files.each { File file ->
      def dest = new File(file.absolutePath.replaceAll(/\.js$/, '.min.js'))

      new JsMinifier().minifyJsFile(
              [file] as Set<File>,
              [] as Set<File>,
              dest,
              project.closure.compilerOptions,
              project.closure.warningLevel,
              project.closure.compilationLevel
      )
    }

    css.source.prod.css.files.each { File file ->
      def dest = new File(file.absolutePath.replaceAll(/\.css$/, '.min.css'))

      new CssMinifier().minifyCssFile(
              file,
              dest,
              project.yuicompressor.lineBreakPos
      )
    }
  }
}

/**
 * JSHint
 */

task jshintTask(type: com.eriwen.gradle.js.tasks.JsHintTask) {
  source = javascript.source.prod.js.files
  dest = "${buildDir}/reports/jshint.xml"
  reporter = 'checkstyle'
}

task csslintTask(type: com.eriwen.gradle.css.tasks.CssLintTask) {
  source = css.source.prod.css.files
  dest = "${buildDir}/reports/csslint.xml"
  csslint {
    format = 'lint-xml'
    errors = []
  }
}

/**
 * Test
 */

task test(type: Exec) {
  def testExecScript = file('assets/www/spec/lib/phantom-jasmine/run_jasmine_test.coffee')
  def testRunner = file('assets/www/spec.html')

  commandLine phantomjs(), testExecScript.absolutePath, testRunner.absolutePath
}

task testReports(type: Exec) {
  def testExecScript = file('assets/www/spec/lib/jasmine-reporters/phantomjs-testrunner.js')
  def testRunner = file('assets/www/spec.html')

  commandLine phantomjs(), testExecScript.absolutePath, testRunner.absolutePath
}

/**
 * JSDoc
 */

task jsdocjs(type: com.eriwen.gradle.js.tasks.JsDocTask) {
  source = fileTree(
          dir: "${projectDir}/assets/www/",
          include: "**/*.js",
          excludes: ['**/cordova*.js', '**/lib/**/*.js']
  ).getFiles().collect { File f -> f.path; }
  destinationDir = file("${buildDir}/jsdoc")
}

/**
 * Phonegap integration
 */

task buildTar(type: Tar, dependsOn: minify) {
  archiveName archiveFile
  compression Compression.GZIP
  includes = [
          'www/',
          'splash.png'
  ]
  excludes = [
          'www/spec.html',
          'www/spec',
          '**/cordova*.js'
  ]
  exclude({
    def ret = false
    def name = it.file.name

    if (name ==~ /.*\.js/ && !(name ==~ /GAPlugin\.js/ || name ==~ /.*(\.|-)min\.js/)) {
      ret = true
    }

    if (name ==~ /.*\.css/ && !(name ==~ /.*\.min\.css/)) {
      ret = true
    }

    ret
  })
  from new File("assets/")

  eachFile { FileCopyDetails fileCopyDetails ->
    if (!fileCopyDetails.isDirectory()) {
      if (fileCopyDetails.name ==~ /.*\.html/) {
        fileCopyDetails.filter { String line ->
          def ret = line.replaceAll(/cordova-.*\.js/, 'phonegap.js')

          // Replace js includes with minified js file
          if (ret ==~ /.*\.js.*/ && !(ret ==~ /.*phonegap\.js.*/ || ret ==~ /.*GAPlugin\.js.*/ || ret ==~ /.*(\.|-)min\.js.*/)) {
            ret = ret.replaceAll(/\.js/, '.min.js')
          }

          // Replace css includes with minified css file
          if (ret ==~ /.*\.css.*/ && !(ret ==~ /.*\.min\.css.*/)) {
            ret = ret.replaceAll(/\.css/, '.min.css')
          }

          ret
        }
      } else if (fileCopyDetails.relativePath.pathString == "www${File.separator}config.xml") {
        fileCopyDetails.filter(ReplaceTokens, tokens: [
                id: project.group + '.' + project.archivesBaseName,
                version: project.version,
                description: project.description
        ])
      }
    }
  }
}

buildTar {
  outputs.upToDateWhen { false }
}

buildTar.doFirst {
  // Remove old zip file
  def zipFile = new File(archiveFile)
  zipFile.delete()
}

task copySplash(type: Copy) {
  from 'res/drawable/splash.png'
  into 'assets'
}

task toPhonegap(dependsOn: [buildTar, copySplash]) {
  doLast {

    if (!phonegapPassword) {
      def console = System.console()
      if (console) {
        phonegapPassword = console.readPassword("\n> Password for ${phonegapUser}: ").toString()
      }
    }

    def host = "build.phonegap.com"
    def apiCall = "https://build.phonegap.com/api/v1/apps/${phonegapAppId}"

    DefaultHttpClient httpclient = new DefaultHttpClient();

    // -- AUTHENTICATION --
    AuthScope authScope = new AuthScope(host, 443);
    UsernamePasswordCredentials upc = new UsernamePasswordCredentials(
            phonegapUser, phonegapPassword);
    ((AbstractHttpClient) httpclient).getCredentialsProvider()
            .setCredentials(authScope, upc);
    BasicHttpContext localContext = new BasicHttpContext();
    BasicScheme basicAuth = new BasicScheme();
    localContext.setAttribute("preemptive-auth", basicAuth);
    HttpHost targetHost = new HttpHost(host, 443, "https");
    // -- AUTHENTICATION END

    // Header info
    httpclient.getParams().setParameter(CoreProtocolPNames.PROTOCOL_VERSION, HttpVersion.HTTP_1_1);
    HttpPut httpPut = new HttpPut(apiCall)
    httpPut.getParams().setBooleanParameter(CoreProtocolPNames.USE_EXPECT_CONTINUE, true)
    httpPut.setHeader("Accept", "*/*")

    // Multipart request - one part containing info to unlock keys, one part containing app file
    MultipartEntity mp = new MultipartEntity(HttpMultipartMode.BROWSER_COMPATIBLE);
    StringBody stringBody = new StringBody("""{"keys":
      {"android":{"id":$androidCertKeyId, "key_pw": "$phonegapKey", "keystore_pw": "$phonegapKey"},
      "ios":{"id":$iOSCertKeyId, "password": "$phonegapKey"}
      }
    }""",
            "application/json", Charset.forName("utf-8"))
    mp.addPart('data', stringBody);
    FileBody fileBody = new FileBody(new File(archiveFile), "binary/octet-stream");
    mp.addPart('file', fileBody);
    httpPut.setEntity(mp); // add the two parts to the request
    // execute request
    HttpResponse response = httpclient.execute(targetHost, httpPut, localContext)

    def status = response.statusLine.statusCode
    def json = new JsonSlurper().parseText(EntityUtils.toString(response.entity))

    if (json?.error?.size() > 0) {
      throw new Exception("Error during publish to phonegap: ${json.error}")
    } else if (status != 200) {
      throw new Exception("Failed to publish to phonegap: (${json.error}) ${status}: ${response.statusLine.reasonPhrase}")
    } else {
      println "Successfully published app '${json.title}' v${json.version} build nr ${json.build_count} to phonegap."
    }
  }
}

/**
 * Config
 */

buildscript {
  repositories {
    maven {
      url = 'http://kellyrob99.github.com/Jenkins-api-tour/repository'
    }
    mavenCentral()
  }
  dependencies {
    classpath 'org.apache.httpcomponents:httpclient:4.2.1'
    classpath 'org.apache.httpcomponents:httpmime:4.2.1'
    classpath 'com.github.timurstrekalov:gradle-saga-plugin:1.4.1'
    classpath 'com.eriwen:gradle-js-plugin:1.5.0'
    classpath 'com.eriwen:gradle-css-plugin:1.2.1'
  }
}

repositories {
  mavenCentral() //needed by the plugin to retrieve the jslint jar
}
